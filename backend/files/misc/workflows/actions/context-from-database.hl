
/*
 * Returns context for the specified [query] from the specified [type],
 * and returns to caller.
 *
 * Optionally add [threshold] and [max_tokens] to specify similarity and maximum
 * tokens to return. [type] is a machine learning type from your ml_types database table in
 * your magic database.
 */
.arguments
   type
      type:string
      mandatory:bool:true
   query
      type:string
      mandatory:bool:true
   threshold
      type:decimal
      mandatory:bool:false
   max_tokens
      type:int
      mandatory:bool:false
.icon:extension

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/type
validators.mandatory:x:@.arguments/*/query

// Applying default values for optional arguments.
validators.default:x:@.arguments
   threshold:decimal:0.3
   max_tokens:int:4000

// Retrieving context.
execute:magic.ai.get-context
   type:x:@.arguments/*/type
   vector_model:text-embedding-ada-002
   prompt:x:@.arguments/*/query
   threshold:x:@.arguments/*/threshold
   max_tokens:x:@.arguments/*/max_tokens

// Returning result of invocation to caller.
yield
   context:x:@execute/*/context


/*
 * Sends an email to the specified [name]/[email] recipient,
 * with the specified [subject] and [body], using the specified [from] and [from-email]
 * as the sender.
 */
.arguments
   name
      type:string
      mandatory:bool:true
   email
      type:email
      mandatory:bool:true
   from
      type:string
      mandatory:bool:true
   from-email
      type:email
      mandatory:bool:true
   subject
      type:string
      mandatory:bool:true
   body
      type:textarea
      mandatory:bool:true
   html
      type:bool
      mandatory:bool:false
.icon:email

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/name
validators.mandatory:x:@.arguments/*/email
validators.email:x:@.arguments/*/email
validators.mandatory:x:@.arguments/*/from
validators.mandatory:x:@.arguments/*/from-email
validators.email:x:@.arguments/*/from-email
validators.mandatory:x:@.arguments/*/subject
validators.mandatory:x:@.arguments/*/body

// Applying defaults.
validators.default:x:@.arguments
   html:bool:false

// Making sure we send email as HTML if we should.
if
   get-value:x:@.arguments/*/html
   .lambda

      // Making sure MIME message becomes HTML Content-Type.
      set-value:x:../*/mail.smtp.send/**/entity
         .:text/html

// Forward evaluating arguments to [mail.smtp.send].
unwrap:x:./*/mail.smtp.send/**

// Sending email.
mail.smtp.send
   message
      to
         .
            name:x:@.arguments/*/name
            email:x:@.arguments/*/email
      from
         .
            name:x:@.arguments/*/from
            email:x:@.arguments/*/from-email
      subject:x:@.arguments/*/subject
      entity:text/plain
         content:x:@.arguments/*/body

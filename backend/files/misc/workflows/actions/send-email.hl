
/*
 * Sends an email to the specified [name]/[email] recipient,
 * with the specified [subject] and [body] using the default from email address and name
 * from your configuration settings.
 */
.arguments
   name
      type:string
      mandatory:bool:true
      default:John Doe
   email
      type:email
      mandatory:bool:true
      default:john@doe.com
   subject
      type:string
      mandatory:bool:true
      default:Some subject line
   body
      type:textarea
      mandatory:bool:true
      default:@"Hello John,

How's it going?"

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/name
validators.mandatory:x:@.arguments/*/email
validators.email:x:@.arguments/*/email
validators.mandatory:x:@.arguments/*/subject
validators.mandatory:x:@.arguments/*/body

// Forward evaluating arguments to [mail.smtp.send].
unwrap:x:./*/mail.smtp.send/**

// Sending email.
mail.smtp.send
   message
      to
         .
            name:x:@.arguments/*/name
            email:x:@.arguments/*/email
      subject:x:@.arguments/*/subject
      entity:text/plain
         content:x:@.arguments/*/body

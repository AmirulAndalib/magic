
/*
 * Execute some select SQL statement specified as [sql].
 * Optionally provide [connection-string], [database-type] and [database].
 */
.arguments
   database-type
      type:enum
      default:sqlite
      mandatory:bool:false
      values
         .:sqlite
         .:mssql
         .:mysql
         .:pgsql
   connection-string
      type:string
      default:generic
      mandatory:bool:false
   database
      type:string
      default:magic
      mandatory:bool:false
   sql
      type:string
      default:select name from roles where name like @arg1 limit 2
      mandatory:bool:true
   params
      type:key-value
      default
         @arg1:a%
      mandatory:bool:false
.example
   database-type:sqlite
   connection-string:generic
   database:magic
   sql:select name from roles where name like @arg1 limit 2
   params
      @arg1:a%
.icon:cloud_download

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/sql

// Applying defaults.
validators.default:x:@.arguments
   connection-string:generic
   database-type:sqlite
   database:magic

// Connection string to actually use as we connect.
.connection-string

// Caller supplied an explicit [connection-string] argument.
set-value:x:@.connection-string
   strings.concat
      .:[
      get-value:x:@.arguments/*/connection-string
      .:|
      get-value:x:@.arguments/*/database
      .:]

// Connecting to database.
data.connect:x:@.connection-string
   database-type:x:@.arguments/*/database-type

   // Parametrizing SQL.
   add:x:./*/data.select
      get-nodes:x:@.arguments/*/params/*

   // Executing SQL.
   data.select:x:@.arguments/*/sql
      database-type:x:@.arguments/*/database-type
   return-nodes:x:@data.select/*

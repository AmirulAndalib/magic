
/*
 * Get database schema
 * 
 * This function returns the schema (DDL) of the specified [database]
 */
.arguments

   // Mandatory argument being database the caller wants to have the schema for
   database:string

// Ensuring user is root.
auth.ticket.verify:root

// Buffer for result
.result

// Connecting to database.
data.connect:x:@.arguments/*/database

   // Executing SQL that returns schema.
   data.select:@"SELECT sql
FROM sqlite_master
WHERE type = 'table'
  AND name NOT LIKE 'sqlite_%';
"

   // Iterating through each result from above to create our schema
   for-each:x:@data.select/*
      set-value:x:@.result
         strings.concat
            get-value:x:@.result
            get-value:x:@.dp/#/*/sql
            .:"\n\n"

// Returning the schema to caller.
yield
   schema:x:@.result

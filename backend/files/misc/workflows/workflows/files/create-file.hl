
/*
 * Creates a new file or modifies the content of an existing file
 * for the specified [module] with the specified [name] and the specified [content].
 * 
 * Arguments:
 * 
 * - [module] being mandatory name of an existing module where the file should be created.
 * - [name] being mandatory filename of the file that should be saved or created.
 * - [content] being mandatory text content for file.
 */
.arguments

   // Mandatory name of module where file should be created.
   module:string

   // Mandatory filename. Must be relative within the module.
   name:string

   // Mandatory text content for file.
   content:string

// Ensuring user is root.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/module
validators.regex:x:@.arguments/*/module
   regex:"^[a-z0-9_-]+$"
validators.string:x:@.arguments/*/content
   min:int:20

// Figuring out full path.
.full_path
set-value:x:@.full_path
   strings.concat
      .:/modules/
      get-value:x:@.arguments/*/module
      .:/
      get-value:x:@.arguments/*/name

// Saving file.
io.file.save:x:@.full_path
   get-value:x:@.arguments/*/content

// Returning success
return
   result:success

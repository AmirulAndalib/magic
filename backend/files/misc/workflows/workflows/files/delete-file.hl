/*
 * Deletes the specified [file] inside the specified [module]
 *
 * Arguments:
 *
 * - [file] being mandatory relative filename that must exist within the [module] module.
 * - [module] being mandatory name of module that contains file.
 */
.arguments

   // Mandatory relative filename of file to delete.
   file:string

   // Mandatory name of module that contains file that should be deleted.
   module:string

// Ensuring user is root.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/module
validators.regex:x:@.arguments/*/module
   regex:"^[a-z0-9_-]+$"
validators.mandatory:x:@.arguments/*/file


// Building full file path.
.full_path
set-value:x:@.full_path
   strings.concat
      .:/modules/
      get-value:x:@.arguments/*/module
      .:/
      get-value:x:@.arguments/*/file

// Verifying file exists
if
   not
      io.file.exists:x:@.full_path
   .lambda
      throw:File does not exist
         public:bool:true
         status:int:400

// Deleting folder and returning success to caller.
io.file.delete:x:@.full_path
return
   result:success

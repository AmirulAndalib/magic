
/*
 * Executes the specified [filename] Hyperlambda file passing in the specified [args] arguments.
 * 
 * Returns result of execution to caller.
 */
.arguments

   // Mandatory name of module where file should be created.
   module:string

   // Mandatory relative path of file to execute.
   filename:string

   // Key/value arguments passed into file as it is executed.
   args:*

// Ensuring user is root.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/filename
validators.mandatory:x:@.arguments/*/module
validators.regex:x:@.arguments/*/module
   regex:"^[a-z0-9_-]+$"

// Figuring out full path.
.full_path
set-value:x:@.full_path
   strings.concat
      .:/modules/
      get-value:x:@.arguments/*/module
      .:/
      get-value:x:@.arguments/*/filename

// Executing file.
add:x:./*/execute-file
   get-nodes:x:@.arguments/*/args/*
execute-file:x:@.full_path

// Returning result to caller.
add:x:./*/return
   get-nodes:x:@execute-file/*
return

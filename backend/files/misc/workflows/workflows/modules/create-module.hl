
/*
 * HTTP endpoint that allows for creating a new module.
 * 
 * Arguments:
 * 
 * - [name], mandatory name of module. Can only contain lower case characters, numbers, underscores, and hyphens.
 */
.arguments

   // Mandatory name of module to create.
   name:string

// Ensuring user is root.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/name
validators.regex:x:@.arguments/*/name
   regex:"^[a-z0-9_-]+$"

// Verifying module doesn't already exist.
io.folder.list:/modules/
for-each:x:@io.folder.list/*
   strings.split:x:@.dp/#
      .:/
   if
      eq:x:@strings.split/0/-
         get-value:x:@.arguments/*/name
      .lambda
         throw:Module already exists
            public:bool:true
            status:int:400

// Creating folder.
strings.concat
   .:/modules/
   get-value:x:@.arguments/*/name
io.folder.create:x:@strings.concat

// Creating manifest.hl file.
.manifest
   version:v1.0.0
   module_name:x:@.arguments/*/name
unwrap:x:-/*
strings.concat
   .:/modules/
   get-value:x:@.arguments/*/name
   .:/manifest.hl
lambda2hyper:x:@.manifest/*
io.file.save:x:@strings.concat
   get-value:x:@lambda2hyper

// Returning success to caller.
return
   result:success

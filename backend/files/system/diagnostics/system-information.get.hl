
// Returns system information to caller.
.description:Returns system information to caller
.type:internal

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Verifying system has been setup.
config.get:"magic:auth:secret"
if
   neq:x:@config.get
      .:THIS-IS-NOT-A-GOOD-SECRET-PLEASE-CHANGE-IT
   .lambda

      // Retrieving version of Magic we're currently running.
      version

      // Retrieving number of persisted tasks.
      tasks.count

      // Counting total number of log items.
      log.count

      // Retrieving endpoints.
      endpoints.list
      .no-endpoints
      set-value:x:-
         get-count:x:@endpoints.list/*

      // Retrieving last 10 log items.
      add:x:@.lambda/*/lambda2hyper/*/last_log_items
         log.query
            max:10

      // Retrieving slots.
      vocabulary
      .no-slots
      set-value:x:-
         get-count:x:@vocabulary/*

      // Retrieving dynamic slots.
      slots.vocabulary
      .no-dynamic-slots
      set-value:x:-
         get-count:x:@slots.vocabulary/*

      // Retrieving default database adapter.
      .default-db
      set-value:x:-
         config.get:"magic:databases:default"

      // Retrieving default timezone.
      .default-timezone
      set-value:x:-
         config.get:"magic:culture:defaultTimeZone"

      // Counting cache items.
      cache.count

      // Verifying scheduler is turned on.
      .has-scheduler:bool:true
      if
         eq
            config.get:"magic:tasks:scheduler"
            .
         .lambda
            set-value:x:@.has-scheduler
               .:bool:false

      // Checking if OpenAI has been configured.
      .has-openai:bool:false
      config.get:"magic:openai:key"
      if
         and
            neq:x:@config.get
               .:
            not-null:x:@config.get
         .lambda
            set-value:x:@.has-openai
               .:bool:true

      // Returning result to caller.
      yield
         version:x:@version
         persisted_tasks:x:@tasks.count
         has_scheduler:x:@.has-scheduler
         log_items:x:@log.count
         cache_items:x:@cache.count
         endpoints:x:@.no-endpoints
         slots:x:@.no-slots
         dynamic_slots:x:@.no-dynamic-slots
         default_db:x:@.default-db
         default_timezone:x:@.default-timezone
         has_openai:x:@.has-openai

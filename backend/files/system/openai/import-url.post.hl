
/*
 * Imports the specified URL as training data by scraping the URL,
 * including all internal hyperlinks, and generate training data
 * from content found.
 */
.arguments
   url:string
   type:string
   delay:int
   max:int
   threshold:int
   summarize:bool
   feedback-channel:string
   

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Prepending scheme unless given.
if
   not
      strings.starts-with:x:@.arguments/*/url
         .:"http"
   .lambda
      set-value:x:@.arguments/*/url
         strings.concat
            .:"https://"
            get-value:x:@.arguments/*/url

// Trimming extra slash.
set-value:x:@.arguments/*/url
   strings.trim-end:x:@.arguments/*/url
      .:/

// Making sure URL does not contain CAPS characters.
set-value:x:@.arguments/*/url
   strings.to-lower:x:@.arguments/*/url

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/url
validators.mandatory:x:@.arguments/*/threshold
validators.url:x:@.arguments/*/url
validators.mandatory:x:@.arguments/*/type
validators.mandatory:x:@.arguments/*/delay
validators.integer:x:@.arguments/*/delay
   min:500
   max:30000
validators.mandatory:x:@.arguments/*/max
validators.integer:x:@.arguments/*/max
   min:1
   max:5000
validators.integer:x:@.arguments/*/threshold
   min:25
validators.default:x:@.arguments
   summarize:bool:false

/*
 * To avoid null reference exceptions in case feedback channel
 * is not provided, we default it to something random.
 */
if
   not-exists:x:@.arguments/*/feedback-channel
   .lambda

      // Creating default dummy value.
      crypto.random
         min:10
         max:20
      unwrap:x:+/*
      validators.default:x:@.arguments
         feedback-channel:x:@crypto.random

// Doing some basic logging.
log.info:Crawling URL generating OpenAI training snippets
   url:x:@.arguments/*/url
   type:x:@.arguments/*/type
   delay:x:@.arguments/*/delay
   max:x:@.arguments/*/max
   threshold:x:@.arguments/*/threshold

// Invoking slot doing the heavy lifting.
unwrap:x:./*/signal/*/.onafter/*/.feedback-channel
add:x:./*/signal
   get-nodes:x:@.arguments/*
signal:magic.ai.crawl-site
   .onafter

      // Informing client that we're done.
      .feedback-channel:x:@.arguments/*/feedback-channel
      sockets.signal:x:@.arguments/*/feedback-channel
         roles:root
         args
            message:Successfull scraped URL(s), remember to vectorize your model before using it
            type:success


// Returning success to caller.
return
   result:success

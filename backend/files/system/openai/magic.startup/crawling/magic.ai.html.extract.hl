
/*
 * Returns Markdown training snippet from specified [html].
 *
 * Takes the following arguments:
 *
 * - [html]     - Mandatory. Being the actual HTML we're traversing for training data.
 * - [url]      - Optional.  Being the URL from where the document was fetched
 */
slots.create:magic.ai.html.extract

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/html
   validators.mandatory:x:@.arguments/*/url
   validators.url:x:@.arguments/*/url

   // Used to return meta information to caller.
   .meta
      main:int:0

   // Buffer used for snippet to return.
   .snippets

   // Buffer for title.
   .title

   // Buffer for description.
   .description

   // Used to store a reference to above HTML transformed to Markdown.
   .markdown

   // Buffer used for URLs from HTML document.
   .urls

   // Converting HTML to Markdown.
   html2markdown:x:@.arguments/*/html
      url:x:@.arguments/*/url
   set-value:x:@.markdown
      get-value:x:@html2markdown

   // Checking if site is SPA, at which point we return early.
   if
      or
         null:x:@.markdown
         eq:x:@.markdown
            .:
      .lambda

         // Nothing to do here ...
         return
            urls
            snippets
            meta
               main:int:0

   // Converting raw HTML to lambda to allow us to extract title, description, hyperlinks, etc.
   .html-lambda
   add:x:@.html-lambda
      html2lambda:x:@.arguments/*/html

   /*
    * Finding URLs from document.
    *
    * Notice, for simplicity reasons we do this by round tripping through HTML,
    * for then to convert HTML to lambda, and iterate through each anchor HTML
    * element in lambda.
    *
    * This is not optimal, and could be optimised, but it keeps the code
    * DRY at least, since our [html2markdown] slot at this point have resolved
    * our relative URLs ...
    */
   markdown2html:x:@html2markdown
   html2lambda:x:@markdown2html
   for-each:x:@html2lambda/**/a/*/\@href

      /*
       * Notice, the URLs we return are for the scraper to crawl and scrape,
       * so we don't return mailto or tel URLs here, and we only return URLs
       * from the same domain.
       */
      if
         and
            not
               strings.starts-with:x:@.dp/#
                  .:"mailto:"
            not
               strings.starts-with:x:@.dp/#
                  .:"tel:"
         .lambda
         
            // Removing '#'.
            .url
            set-value:x:@.url
               get-value:x:@.dp/#
            strings.split:x:@.url
               .:#
            set-value:x:@.url
               get-value:x:@strings.split/0

            // Valid URL, now checking if it's the same domain.
            .local
            strings.split:x:@.arguments/*/url
               .:"/"
            set-value:x:@.local
               get-value:x:@strings.split/1
            .current
            strings.split:x:@.url
               .:"/"
            set-value:x:@.current
               get-value:x:@strings.split/1
            if
               eq:x:@.local
                  get-value:x:@.current
               .lambda

                  // Local URL, now making sure it's not the same URL.
                  strings.trim-end:x:@.url
                     .:/
                  strings.trim-end:x:@.arguments/*/url
                     .:/
                  if
                     neq:x:@strings.trim-end
                        get-value:x:@strings.trim-end/@strings.trim-end
                     .lambda
                     
                        // Not the same URLas the one we'recurrently scraping.
                        unwrap:x:+/*/*
                        add:x:@.urls
                           .
                              .:x:@.url

   // Setting title and description from document.
   set-value:x:@.title
      get-value:x:@.html-lambda/**/head/**/title/*/\#text
   set-value:x:@.description
      get-value:x:@.html-lambda/**/head/**/meta/*/\@name/=description/./*/\@content

   // Creating our prompt.
   .prompt
   set-value:x:@.prompt
      get-first-value
         get-value:x:@.title
         get-value:x:@html2lambda/**/h1/[0,1]/*/\#text
         get-value:x:@html2lambda/**/h2/[0,1]/*/\#text
         get-value:x:@html2lambda/**/h3/[0,1]/*/\#text
         get-value:x:@html2lambda/**/h4/[0,1]/*/\#text
         get-value:x:@html2lambda/**/h5/[0,1]/*/\#text
         get-value:x:@html2lambda/**/h6/[0,1]/*/\#text
         get-value:x:@.description
         .:"Page"

   // Breaking page into sections.
   .remaining
   .tmp-prompt
   set-value:x:@.tmp-prompt
      get-value:x:@.prompt
   for-each:x:@html2lambda/*

      get-name:x:@.dp/#
      switch:x:@get-name

         case:h1
         case:h2
         case:h3
         case:h4
         case:h5
         case:h6
            set-value:x:@.tmp-prompt
               get-value:x:@.dp/#/*/\#text

         case:ul
         case:ol
         case:pre

            /*
             * To avoid repeating navbars "everywhere" we remove these from Markdown and returns
             * these as individual training snippets. This will ensure navbars are only imported
             * once, as a separated training snippet.
             */
            lambda2html:x:@.dp/#
            html2markdown:x:@lambda2html
               url:x:@.arguments/*/url
            strings.concat
               get-value:x:@.title
               .:" | "
               get-value:x:@.tmp-prompt
            unwrap:x:+/*/*/*
            add:x:@.snippets
               .
                  .
                     prompt:x:@strings.concat
                     completion:x:@html2markdown

         default

            // Adding remaining HTML, minus the parts we removed.
            add:x:@.remaining
               get-nodes:x:@.dp/#

   lambda2html:x:@.remaining/*
   set-value:x:@.markdown
      html2markdown:x:@lambda2html
         url:x:@.arguments/*/url

   .completion
   set-value:x:@.completion
      get-value:x:@.markdown
   unwrap:x:+/*/*/*
   add:x:@.snippets
      .
         .
            prompt:x:@.prompt
            completion:x:@.completion

   // Returning snippets to caller if we could find anything.
   if
      exists:x:@.snippets/*
      .lambda

         // Returning snippets and meta information to caller.
         add:x:./*/return/*/snippets
            get-nodes:x:@.snippets/*
         add:x:./*/return/*/meta
            get-nodes:x:@.meta/*
         add:x:./*/return/*/urls
            get-nodes:x:@.urls/*
         return
            urls
            snippets
            meta

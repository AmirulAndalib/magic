
/*
 * Vectorises all training snippets in the specified model/type.
 */
.arguments
   type:string
   feedback-channel:string

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/type

/*
 * To avoid null reference exceptions in case feedback channel
 * is not provided, we default it to something random.
 */
if
   not-exists:x:@.arguments/*/feedback-channel
   .lambda

      // Creating default dummy value.
      crypto.random
         min:10
         max:20
      unwrap:x:+/*
      validators.default:x:@.arguments
         feedback-channel:x:@crypto.random

// Doing some basic logging.
log.info:Started vectorising model
   model:x:@.arguments/*/type

// Creating a thread and invoking file doing the heavy lifting.
insert-before:x:./*/fork/0
   get-nodes:x:@.arguments
fork
   add:x:./*/signal
      get-nodes:x:@.arguments/*
   signal:magic.ai.vectorise

// Returning success to caller.
return
   result:success
